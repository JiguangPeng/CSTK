#!/usr/bin/env perl

=hey
Author: Shijian Sky Zhang
E-mail: zhangsjsky@pku.edu.cn
=cut

use 5.012;
use warnings;
use Getopt::Long;
use File::Basename;

my $prefix = 'R';
my $increment = 1;
my $mapBack;

sub usage{
    my $scriptName = basename $0;
print <<HELP;
Usage: perl $scriptName INPUT.sam/bam >OUTPUT.sam
    If INPUT.sam/bam isn't specified, input from STDIN
Option:
    -p --prefix     STR The prefix of mapped name[$prefix]
    -i --increment  INT The initial number of the increment[$increment] 
    -b --mapBack    TSV File with columns of new and original names.
                        It can be the STDERR generated by $scriptName without -b.
                        New name in the INPUT file will be mapped back to original name.
    -h --help           Print this help information
HELP
    exit(-1);
}

GetOptions(
            'p|prefix=s'    => \$prefix,
            'i|increment=i' => \$increment,
            'b|mapBack=s'   => \$mapBack,
            'h|help'        => sub{usage()}
        ) || usage();

if(defined $ARGV[0]){
    if (-B $ARGV[0]){
        open IN, "samtools view -h $ARGV[0]|" or die "Can't open $ARGV[0]: $!";
    }else{
        open IN, "$ARGV[0]" or die "Can't open $ARGV[0]: $!";
    }
}else{
    open IN, "-";
}

my %mapBack;
if(defined $mapBack){
    open MB, "$mapBack" or die "Can't open $mapBack: $!";
    while(<MB>){
        chomp;
        my ($newName, $oriName) = split "\t";
        $mapBack{"$newName"} = $oriName;
    }
}

while(<IN>){
    chomp;
    if(/^@/){
        say;
        next;
    }
    my @fields = split "\t";
    my ($name, @tags) = @fields[0, 11..$#fields];
    if(defined $mapBack){
        $name = $mapBack{"$name"};
    }else{
        say STDERR join "\t", ("$prefix$increment", $name);
        $name = "$prefix$increment";
        $increment++;
    }
    $fields[0] = $name;
    say join "\t", @fields;
}
